;
; Front end for MDFourier tone generator in 144p Test Suite
; Copyright 2020, 2023 Damian Yerrick
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License along
; with this program; if not, write to the Free Software Foundation, Inc.,
; 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
;
include "src/gb.inc"
include "src/global.inc"

section "mdfourierfe",ROM0

;;
; make the background dark on GBC and light on DMG  
mdfourier_blank:
  ld a, $FF
  call set_bgp
  xor a
  ldh [rBGP], a
  fallthrough mdfourier_screen_on
mdfourier_screen_on:
  ld a,LCDCF_ON|BG_NT0|BG_CHR21
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a
  ret

activity_mdfourier::
  call clear_gbc_attr
  call mdfourier_blank
  call mdfourier_ready_tone
.restart:
  call clear_gbc_attr
  ld hl, CHRRAM2
  ld c, 16
  rst memset_tiny  ; clear background tile
  call clear_scrn0_to_0
  ld de,CHRRAM1>>4
  ld hl,mdfourier_title_labels
  call vwfDrawLabels
  ld a, %01011100
  call set_bgp
  call mdfourier_screen_on
  ld b, 7
  call sgb_set_palette_b

.loop:
  ld b,helpsect_mdfourier
  call read_pad_help_check
  jr nz, .restart
  rst wait_vblank_irq
  ldh a, [new_keys]
  bit PADB_B, a
  ret nz
  rra
  jr nc, .loop

  call mdfourier_blank
  call run_mdfourier
  jr .restart

mdfourier_title_labels:
  db  24, 32,"MDFourier v1",10
  db  24, 40,"tone generator",10
  db  24, 64,"A: Start   B: Stop",0
